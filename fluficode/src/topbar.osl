
boolean debug = false
array defaultButtons = [
  {
    icon: "close",
    click: window.close
  },
  {
    icon: "down",
    click: window.minimise
  },
  {
    icon: "maximise",
    click: window.fullscreen
  }
]
array defaultPopupButtons = [
  {
    icon: "close",
    click: window.close
  }
]

boolean menusOpen = false

def updateWinButtons(array buttons, boolean background) (
  if background (
    local w = buttons.len * 25
    
    loc -2 2 w / -2 - 7.5 -20
    square w - 5 18 10 : c#prim
  )
  
  loc -2 2 -20 -20
  change_x 25
  local i = 0
  local len = buttons.len
  while i < len (
    i ++
  //for i buttons.len (
    local button @= buttons[i]
    
    c txtc
    icon button.icon 0.6 * (button.icon_size ?? 1) : hover_size#1.1 chx#-25
    
    if mouse_touching (
      cursor "pointer"
      if onclick (
        void button.click()
      )
    )
  )
  change_x -25
  self.buttonsX = x_position
)

def updateWidgets(array widgets, string side) (
  local mul = side == "right" ? -1 1
  local x = side == "right" ? buttonsX window.left + 50 + 35
  local visualI = 0
  for i widgets.len (
    local widget @= widgets[i]
    if widget.earlyUpdate != null (
      void widget.earlyUpdate()
    )
    if widget.enabled ?? true (
      visualI ++
      local w = widget.width ?? 20
      
      if visualI != 1 (
        x += (widget.background ? 10 7.5) * mul
      ) else (
        x += widget.background ? 0 -10 * mul
      )
      x += w / 2 * mul
      
      goto x window.top - 20
      if widget.background and widget.showBackground ?? true (
        square w - 5 15 15 : c#prim
      )
      
      square w - 5 15 15 0 1
      widget.hovered = mouse_touching
      widget.clicked = onclick
      
      frame x - (w / 2) - 5 window.top - 5 x + (w / 2) + 5 window.top - 35 (
        if widget.update != null (
          c txtc
          goto 0 0
          void widget.update()
        )
      )
      
      x += w / 2 * mul
      x += (widget.background ? 10 7.5) * mul
    )
  )
  
  if side == "left" (
    self.dragbox[1][3] = x - window.left - 5
  )
  if side == "right" (
    self.dragbox[2][3] = x - window.right - 5
  )
)

def updateLeftSection() (
  goto window.left + 20 window.top - 20
  
  if !self.menusOpen (
    square 25 25 0 0 1 : c#fff
    icon icons.editor 1.25
    change_x 35
  )
  
  icon icons.menu 1 : c#txtc
  if mouse_touching (
    cursor "pointer"
    if onclick (
      self.menusOpen = !self.menusOpen
    )
  )
)

def updateMenus() (
  local x = frame.left
  
  local keys @= menus.topbar.top_menus.getKeys()
  for i keys.len (
    local name = keys[i].toTitle()
    local width = name.len * 9 + 20
    
    x += width / 2 + 7.5
    
    goto x 0
    square width - 5 15 15 10 : c#prim hover_c#seco
    local click = onclick
    centext name 9 : c#txtc
    
    x += width / 2 + 10
    
    x -= width + 12.5
    
    goto x - 5 -25
    if click (
      local p @= Promise.new(() -> (
        defer
        while mouse_down (
          defer
        )
        open_rightclick self.menu() ?? []
      ))
      p.worker.menu @= menus.topbar.top_menus[keys[i]]
    )
    
    x += width + 12.5
  )
  
  self.dragbox[1][3] = x - frame.left + 40
)

def resetDragbox() (
  self.dragbox @= [[2,2,0,0],[-2,2,-90,-40]]
)

def applyDragbox() (
  window.setDragbox(self.dragbox[1], self.dragbox[2])
  
  if self.debug (
    c #fff
    loc self.dragbox[1][1] self.dragbox[1][2] self.dragbox[1][3] self.dragbox[1][4]
    pen "down" : w#1
    loc self.dragbox[2][1] self.dragbox[2][2] self.dragbox[2][3] self.dragbox[2][4]
    pen "up"
  )
)

def update() (
  void self.resetDragbox()
  
  void self.updateWinButtons(self.defaultButtons, false)
  if self.menusOpen (
    frame window.left + 40 window.top self.buttonsX window.top - 40 (
      void self.updateMenus()
    )
  ) else (
    void self.updateWidgets(widgets.left, "left")
  )
  void self.updateWidgets(widgets.right, "right")
  void self.updateLeftSection()
  
  void self.applyDragbox()
)
