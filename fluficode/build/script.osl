build @= { built: true, package: {"name":"editor","version":"1.0.0","description":"custom editor because i got bored of studio","main":"script.osl","dependencies":{},"tags":[],"include":[]} }
opal_imports = {}
def import(path) (
  switch path (

case "./widgets/run.osl"
  class __imports__7b17b4f0ec5dec079dbf75a42dd885a6 (
    number width = 20
    boolean background = true
        
    def earlyUpdate() (
      local f @= utils.getMain()
      
      self.enabled = f != null
    )
    
    def update() (
      goto 0 0
      icon icons.play .6
      
      if self.hovered (
        cursor "pointer"
        if self.clicked (
          local f @= utils.getMain()
          if f != null (
            file "open" f
            file "start"
            file "close"
          )
        )
      )
    )
  )
  return __imports__7b17b4f0ec5dec079dbf75a42dd885a6
case "./widgets/folder.osl"
  class __imports__6ea7205d63c0854cbd1418798a22ae00 (
    boolean enabled = false
    boolean background = true
    
    def earlyUpdate() (
      local folder @= open(state.folder)
      self.enabled = folder != null
    )
    
    def update() (
      local folder = open(state.folder, [2, 3])
      self.width = folder[1].len * 10 + 10
      goto 0 0
      centext folder[1] 10 : c#txtc
      if mouse_touching (
        cursor "pointer"
        if onclick (
          local p @= Promise.new(() -> (
            defer
            while mouse_down (
              defer
            )
            open_rightclick menus.topbar.name() self.folder[2] ++ "/" ++ self.folder[1]
          ))
          p.worker.folder @= folder
        )
      )
    )
  )
  return __imports__6ea7205d63c0854cbd1418798a22ae00
case "./icon.icn"
  return "  c #691f7d c #bf58db w 10 square 0 0 5 5 c #111 w 8 square 0 0 5 5 dot 0 0 c #bf58db w 1.5 line -6 0 -6 4 cutcircle -4 4 2 -4.5 45 line -4 6 -3 6 line -6 2.5 -4 2.5 cutcircle 2 2 2 -13.5 45 line 0 2 0 4 cutcircle 2 4 2 -4.5 45 line 2 0 2.5 0 line 2 6 2.5 6 w 2 line -5.75 -5 0 -5"

case "./src/popup.osl"
  class __imports__61f43d578401814c74122435d0e6eb11 (
    object popups = {
      testPopup: {
        update: () -> (
          centext "hi" 10
        )
      }  
    }
    
    def openPopup(object popup) (
      window.create(window.file.uuid, {
        passed_data: {
          popup
        },
      })
    )
    
    def update() (
      void self.updateTopbar()
    )
    
    def updateTopbar() (
      //void topbar.resetDragbox()
      
      void topbar.updateWinButtons(topbar.defaultButtons, true)
    )
  )
  return __imports__61f43d578401814c74122435d0e6eb11
case "./src/picker.osl"
  class __imports__e7e0c81ced745ae6e1b615bbfa83b58f (
    object pickers = {}
    
    def update() (
      if new_transmit (
        local keys @= pickers.getKeys()
        for i keys.len (
          if transmit_source == keys[i] (
            local p @= pickers[keys[i]]
            void p[1](open(transmit_data), transmit_data, p[2])
            void pickers.delete(keys[i])
          )
        )
        new_transmit = false
      )
    )
    
    def openPicker(array types, function callback, arg) (
      file "pick" types
      picker.pickers[data] @= [callback, arg]
    )
  )
  return __imports__e7e0c81ced745ae6e1b615bbfa83b58f
case "./src/settings.osl"
  class __imports__eed2d8cf3c2aa3bfe5551e9b06c28d44 (
    object entries = {
      test: {
        bleh: {
          type: "boolean",
          desc: "fr",
          default: false
        }
      }
    }
    
    object _data = {}
    
    def load() (
      if !save.exists("settings.json") (
        void self.write()
      )
      self._data @= self.deserialize(save.get("settings.json"))
      void self.applySettings()
      void self.write()
    )
    
    def write() (
      void save.set("settings.json", self.serialize())
    )
    
    def get(string category, string entry) (
      return self._data[category][entry]
    )
    def set(string category, string entry, value) (
      local entryData @= self.entries[category][entry]
      if entryData == null (
        throw "setting" + category ++ "/" ++ entry + "doesnt exist"
      )
      
      if entryData.type != typeof(value) (
        throw "expected" + entry.type + "for" + category ++ "/" ++ entry + "but got" + typeof(value)
      )
      self._data[category][entry] @= value
      void self.write()
      void self.applySettings()
    )
    
    def applySettings() (
      local categoryData @= self.entries.getEntries()
      for categoryI categoryData.len (
        local category @= categoryData[categoryI]
        local entryData @= category[2].getEntries()
        for entryI entryData.len (
          local entry @= entryData[entryI]
          local value = self._data[category[1]][entry[1]] ?? entry[2].default
          if entry[2].applySetting != null (
            void entry[2].applySetting(value)
          )
        )
      )
    )
    
    def serialize() (
      local out @= {}
      local categoryData @= self.entries.getEntries()
      for categoryI categoryData.len (
        local category @= categoryData[categoryI]
        out[category[1]] @= {}
        local entryData @= category[2].getEntries()
        for entryI entryData.len (
          local entry @= entryData[entryI]
          local value = self._data[category[1]][entry[1]] ?? entry[2].default
          out[category[1]][entry[1]] @= value
        )
      )
      return out.JsonStringify()
    )
    def deserialize(string data) (
      local raw @= data.JsonParse()
      
      local out @= {}
      local categoryData @= self.entries.getEntries()
      for categoryI categoryData.len (
        local category @= categoryData[categoryI]
        out[category[1]] @= {}
        local entryData @= category[2].getEntries()
        for entryI entryData.len (
          local entry @= entryData[entryI]
          local value = raw[category[1]][entry[1]] ?? entry[2].default
          out[category[1]][entry[1]] @= value
        )
      )
      return out
    )
  )
  return __imports__eed2d8cf3c2aa3bfe5551e9b06c28d44
case "./src/save.osl"
  class __imports__cc67bf7e28ec083154c7e851dbd8117f (
    def init() (
      void self.setName("code@flufi")
    )
    
    def setName(string name) (
      save name "set_directory"
    )
    
    def exists(string fileName) (
      return fileName.saveExists()
    )
    
    def get(string fileName) (
      if !self.exists(fileName) (
        throw fileName ++ "does not exist"
      )
    
      return fileName.saveGet().toStr()
    )
    
    def set(string fileName, string data) (
      save fileName "set" data
    )
  )
  return __imports__cc67bf7e28ec083154c7e851dbd8117f
case "./src/menus.osl"
  class __imports__25b12eaccc0fc7b33e81f6504e5d7022 (
    def createMenu() (
      return []
    )
    def addButton(array menu, string text, function func) (
      local obj @= menus.empty()
      obj[text.toTitle()] @= func
      void menu.append(obj)
    )
    def addSeperator(array menu) (
      void menu.append("-")
    )
    
    empty @= () -> (
      local obj @= {"bleh":def()->()}
      void obj.delete("bleh")
      return obj
    )
    
    object topbar = {
      name: () -> (
        local menu @= menus.createMenu()
        
        void menus.addButton(menu, "copy folder path", (data) -> (
          clipboard "set" data.replace("origin/(c) users/" ++ username, "~")
        ))
        
        return menu
      ),
      
      top_menus: {
        file: () -> (
          local menu @= menus.createMenu()
          
          void menus.addButton(menu, "new", () -> (
            topbar.menusOpen = false
            
            void state.clearFolder()
          ))
          
          void menus.addSeperator(menu)
          
          void menus.addButton(menu, "open file", (data) -> (
            topbar.menusOpen = false
            
            void picker.openPicker([], def(content, uuid) -> (
              local editor @= utils.getEditorAndAdd()
              if editor != null (
                void editor.openFile(uuid)
              )
            ))
          ))
          void menus.addButton(menu, "open folder", (data) -> (
            topbar.menusOpen = false
            
            void picker.openPicker(["folder"], def(content, uuid) -> (
              void state.loadFolder(uuid)
            ))
          ))
          
          return menu
        ),
        edit: () -> (
          local menu @= menus.createMenu()
          
          return menu
        )
      }
    }
    
    object editor = {
      more: () -> (
        log menus
        local menu @= menus.createMenu()
        
        void menus.addButton(menu, "new editor to the left", () -> (
          void layout.editors.insert(editor.selectedEditor, editor.Editor())
        ))
        void menus.addButton(menu, "new editor to the right", () -> (
          void layout.editors.insert(editor.selectedEditor + 1, editor.Editor())
        ))
        
        if layout.editors.len > 1 (
          void menus.addSeperator(menu)
          void menus.addButton(menu, "remove Editor", () -> (
            void layout.editors.delete(editor.selectedEditor)
          ))
        )
        
        return menu
      )
    }
  )
  return __imports__25b12eaccc0fc7b33e81f6504e5d7022
case "./src/utils.osl"
  class __imports__4500036a1de35802b8dcb4fd720972a2 (
    def getEditorAndAdd() (
      if layout.editors.len == 0 (
        void layout.editors.append(editor.Editor())
      )
      return getEditor()
    )
    def getEditor() (
      local editorI = editor.selectedEditor ?? 1
      local editor @= layout.editors.filter(e -> e.i == editorI)[1]
      return editor
    )
    
    def getTab() (
      local e @= getEditor()
      return e.tabs[e.selectedTab]
    )
    
    def getMain() (
      void state.loadOpal()
      
      local opalMain = state.opal.main
      if typeof(opalMain) == "string" (
        local folder @= open(state.folder, [2,3])
        return folder[2] ++ "/" ++ folder[1] ++ "/" ++ opalMain
      )
      
      return self.getTab().file_uuid
    )
  )
  return __imports__4500036a1de35802b8dcb4fd720972a2
case "./src/ohf.osl"
  class __imports__897e4e2423deb74a5324dd50b0eaea87 (
    def compileOhf(ohf) (
      if typeof(ohf) == "string" (
        ohf @= ohf.JsonParse()
      )
      if typeof(ohf) != "object" (
        return ""
      )
      
      local output @= {
        tokens: {},
        overrides: {},
        parser: {}
      }
    
      try (
        if ohf.categories != null (
          each o ohf.categories.getValues() (
            each r o.tokens (
              output.tokens["tkn:" ++ r] = o.color
            )
          )
        )
      
        if ohf.overrides != null (
          each o ohf.overrides.getKeys() (
            output.overrides[o] = ohf.overrides[o]
          )
        )
      
        output.parser = ohf.parser ?? {}
        output.highlighter = ohf.highlighter ?? {}
      ) catch o (
        error o
        return ""
      )
      return output
    )
    
    def getCompiledOhf(string kind) (
      return compileOhf(getOhf(kind))
    )
    
    def getOhf(string kind) (
      return open("~/application data/Studio/" ++ kind.trim(2,-1) ++ ".ohf")
    )
  )
  return __imports__897e4e2423deb74a5324dd50b0eaea87
case "./src/files.osl"
  class __imports__6c459cf0012bdb7372e3f2253de450a7 (
    number width = 200
    number height = 0
    object opened = {}
    
    number x = 0
    number y = 0
    
    def update(array a) (
      goto area.centerX(a) area.centerY(a)
      square area.width(a) area.height(a) : c#prim
      square area.width(a) - 3 area.height(a) - 3 : c#window_colour
      
      c prim
      frame a[1] a[2] a[3] a[4] self.height "files" (
        local hover = mouse_x >= frame.left and mouse_x <= frame.right
        hover = hover and mouse_y >= frame.bottom and mouse_y <= frame.top
        self.hovered = hover
        if file_dragging and hover (
          centext "drop folder here" 9 : c#txtc
          return
        )
        
        local folder @= open(state.folder)
        if typeof(folder) != "array" (
          centext "no open folder" 9 : c#prim
          return
        )
        
        self.x = frame.left
        self.y = frame.top + frame.scroll
        local start = self.y
        drawContents(folder)
        self.height = start - self.y
      )
    )
    
    def drawContents(array items) (
      items @= items.map(i -> [i, open(i, [1])])
      
      local folders @= items.filter(i -> i[2][1] == ".folder")
      local files @= items.filter(i -> i[2][1] != ".folder")
      
      for i folders.len (
        drawFile(folders[i][1])
      )
      for i files.len (
        drawFile(files[i][1])
      )
      // stops compiler from being weird
      return
      square
    )
    
    def drawFile(string uuid) (
      self.y -= 15
      
      local f @= open(uuid, [1,2,3,11])
      
      goto 0 self.y
      square frame.width 20 10 0 1
      rightclick "file" uuid
      local hover = mouse_touching and !layout.filesResizing and !layout.filesResizingHover
      local click = onclick and !layout.filesResizing
      
      if hover (
        square frame.width - 15 15 10 : c#prim
        cursor "pointer"
      )
      
      goto self.x self.y
      
      if f[1] == ".folder" (
        local open = self.opened[uuid] ?? false
        
        icon open ? "down" "right" .45 : chx#15 c#txtc
        file "render" .6 : chx#20
        text f[2] 9 : c#txtc chx#15
        
        self.x += 20
        self.y -= 15
        
        if open (
          drawContents(open(uuid))
        )
        
        self.x -= 20
        
        if click (
          self.opened[uuid] = !open
        )
      ) else (
        file "render" .6 : chx#15
        text f[2] 9 : c#txtc chx#15
        text f[1] 8 : c#seco
        self.y -= 15
        
        if click (
          local editor @= utils.getEditorAndAdd()
          if editor != null (
            void editor.openFile(uuid)
          )
        )
      )
    )
  )
  return __imports__6c459cf0012bdb7372e3f2253de450a7
case "./src/state.osl"
  class __imports__7576a2b49d81733616f3acd68d545597 (
    string folder = ""
    object opal = {}
    
    def loadFolder(string uuid) (
      if uuid == "" (
        self.clearFolder()
        return
      )
      
      local f @= open(uuid, [1,2,3])
      if f[1] != ".folder" (
        say "given folder is not a folder"
        return
      )
      
      self.folder = uuid
      loadOpal()
    )
    
    def clearFolder() (
      self.folder = ""
      self.opal @= {}
    )
    
    def loadOpal() (
      local f @= open(self.folder, [2,3])
      local opalf @= open(f[2] ++ "/" ++ f[1] ++ "/" ++ "opal.json")
      if typeof(opalf) == "string" (
        opalf @= opalf.JsonParse()
      )
      if typeof(opalf) == "object" (
        self.opal @= opalf
      )
    )
  )
  return __imports__7576a2b49d81733616f3acd68d545597
case "./src/topbar.osl"
  class __imports__ec116b9cd375e5297768166f7fffe095 (
    boolean debug = false
    array defaultButtons = [
      {
        icon: "close",
        click: window.close
      },
      {
        icon: "down",
        click: window.minimise
      },
      {
        icon: "maximise",
        click: window.fullscreen
      }
    ]
    
    boolean menusOpen = false
    
    def updateWinButtons(array buttons, boolean background) (
      if background (
        local w = buttons.len * 25
        
        loc -2 2 w / -2 - 7.5 -20
        square w - 5 18 10 : c#prim
      )
      
      loc -2 2 -20 -20
      change_x 25
      local i = 0
      local len = buttons.len
      while i < len (
        i ++
      //for i buttons.len (
        local p = performance
        local button @= buttons[i]
        local p2 = performance
        
        c txtc
        local p3 = performance
        icon button.icon 0.6 * (button.icon_size ?? 1) : hover_size#1.1 chx#-25
        
        local p4 = performance
        if mouse_touching (
          cursor "pointer"
          if onclick (
            void button.click()
          )
        )
        local p5 = performance
        if background (
          log p6 p p - p6
        )
        local p6 = performance
      )
      change_x -25
      self.buttonsX = x_position
    )
    
    def updateWidgets(array widgets, string side) (
      local mul = side == "right" ? -1 1
      local x = side == "right" ? buttonsX window.left + 50 + 35
      local visualI = 0
      for i widgets.len (
        local widget @= widgets[i]
        if widget.earlyUpdate != null (
          void widget.earlyUpdate()
        )
        if widget.enabled ?? true (
          visualI ++
          local w = widget.width ?? 20
          
          if visualI != 1 (
            x += (widget.background ? 10 7.5) * mul
          ) else (
            x += widget.background ? 0 -10 * mul
          )
          x += w / 2 * mul
          
          goto x window.top - 20
          if widget.background and widget.showBackground ?? true (
            square w - 5 15 15 : c#prim
          )
          
          square w - 5 15 15 0 1
          widget.hovered = mouse_touching
          widget.clicked = onclick
          
          frame x - (w / 2) - 5 window.top - 5 x + (w / 2) + 5 window.top - 35 (
            if widget.update != null (
              c txtc
              goto 0 0
              void widget.update()
            )
          )
          
          x += w / 2 * mul
          x += (widget.background ? 10 7.5) * mul
        )
      )
      
      if side == "left" (
        self.dragbox[1][3] = x - window.left - 5
      )
      if side == "right" (
        self.dragbox[2][3] = x - window.right - 5
      )
    )
    
    def updateLeftSection() (
      goto window.left + 20 window.top - 20
      
      if !self.menusOpen (
        square 25 25 0 0 1 : c#fff
        icon icons.editor 1.25
        change_x 35
      )
      
      icon icons.menu 1 : c#txtc
      if mouse_touching (
        cursor "pointer"
        if onclick (
          self.menusOpen = !self.menusOpen
        )
      )
    )
    
    def updateMenus() (
      local x = frame.left
      
      local keys @= menus.topbar.top_menus.getKeys()
      for i keys.len (
        local name = keys[i].toTitle()
        local width = name.len * 9 + 20
        
        x += width / 2 + 7.5
        
        goto x 0
        square width - 5 15 15 10 : c#prim hover_c#seco
        local click = onclick
        centext name 9 : c#txtc
        
        x += width / 2 + 10
        
        x -= width + 12.5
        
        goto x - 5 -25
        if click (
          local p @= Promise.new(() -> (
            defer
            while mouse_down (
              defer
            )
            open_rightclick self.menu() ?? []
          ))
          p.worker.menu @= menus.topbar.top_menus[keys[i]]
        )
        
        x += width + 12.5
      )
      
      self.dragbox[1][3] = x - frame.left + 40
    )
    
    def resetDragbox() (
      self.dragbox @= [[2,2,0,0],[-2,2,-90,-40]]
    )
    
    def applyDragbox() (
      window.setDragbox(self.dragbox[1], self.dragbox[2])
      
      if self.debug (
        c #fff
        loc self.dragbox[1][1] self.dragbox[1][2] self.dragbox[1][3] self.dragbox[1][4]
        pen "down" : w#1
        loc self.dragbox[2][1] self.dragbox[2][2] self.dragbox[2][3] self.dragbox[2][4]
        pen "up"
      )
    )
    
    def update() (
      void self.resetDragbox()
      
      void self.updateWinButtons(self.defaultButtons, false)
      if self.menusOpen (
        frame window.left + 40 window.top self.buttonsX window.top - 40 (
          void self.updateMenus()
        )
      ) else (
        void self.updateWidgets(widgets.left, "left")
      )
      void self.updateWidgets(widgets.right, "right")
      void self.updateLeftSection()
      
      void self.applyDragbox()
    )
  )
  return __imports__ec116b9cd375e5297768166f7fffe095
case "./src/icons.osl"
  class __imports__390660650b3083b6bb77ea5300ce655c (
    string build = "w 2 square 0 -4 2 6 line 6 10 9 10 cont 9 4 cont 6 4 cutcircle 4.5 10 1.5 13.5 45 cutcircle 4.5 4 1.5 4.5 45 line 4.5 8.5 2 8.5 line 4.5 5.5 2 5.5 cont 2 0 cutcircle .5 8.5 1.5 4.5 45 line .5 10 -4 10 curve -4 10 -10 5 -7 10 curve -10 5 -2 4 -2 7 line -2 2 -2 4"
    string play = "w 3.5 line -7 10 7 0 cont -7 -10 cont -7 10"
    string menu = "w 3.85 line -9 8 9 8 line -9 0 9 0 line -9 -8 9 -8"
    
    string editor = import("./icon.icn")
  )
  return __imports__390660650b3083b6bb77ea5300ce655c
case "./src/layout.osl"
  class __imports__805aadf4abdd4a38d6061c6bb7905445 (
    array editors @= [
      editor.Editor(),
    ]
    
    def update() (
      local screen @= area.boundriesToArea(window.left, window.top, window.right, window.bottom)
      
      screen[2] -= 45
      
      self.filesResizingHover = false
      if mouse_y > screen[4] and mouse_y < screen[2] (
        if abs(screen[1] + files.width - mouse_x) < 5 (
          cursor "col-resize"
          self.filesResizingHover = true
          goto mouse_x mouse_y
          square 100 100 0 0 1
          if onclick (
            self.filesResizing = true
          )
        )
      )
      
      if !mouse_down (
        self.filesResizing = false
      )
      
      if self.filesResizing (
        files.width = mouse_x - screen[1]
        cursor "col-resize"
      )
      files.width = min(files.width, area.width(screen) - 200)
      files.width = max(files.width, 150)
      
      void files.update([
        screen[1],
        screen[2],
        screen[1] + files.width,
        screen[4]
      ])
      
      local editorSpace @= area.copy(screen)
      editorSpace[1] += files.width
      updateEditors([
        screen[1] + files.width,
        screen[2],
        screen[3],
        screen[4]
      ])
    )
    
    def updateEditors(array editorSpace) (
      local x = editorSpace[1]
      local sel = null
      
      if self.editors.len == 0 (
        void self.editors.append(editor.Editor())
      )
      
      if editor.selectedEditor > self.editors.len (
        editor.selectedEditor = self.editors.len
      )
      
      for i self.editors.len (
        local curEditor @= self.editors[i]
        local width = area.width(editorSpace) / self.editors.len
        
        curEditor.layoutI = i
        
        local a @= [x, editorSpace[2], x + width, editorSpace[4]]
        
        void curEditor.update(a)
        if curEditor.layoutI == editor.selectedEditor (
          sel @= a
        )
        
        x += width
      )
      
      // kinda looks weird imo, could add a setting tho
      if sel != null and false (
        goto sel[1] sel[2]
        pen "down" : c#seco w#2
        goto sel[3] sel[2]
        goto sel[3] sel[4]
        goto sel[1] sel[4]
        goto sel[1] sel[2]
        pen "up"
      )
    )
  )
  return __imports__805aadf4abdd4a38d6061c6bb7905445
case "./src/editor.osl"
  class __imports__43f488fefabf2ce67b06a4b7a37dc437 (
    number editorI = 0
    number selectedEditor = 0
    number tabI = 0
    
    class Editor (
      number tabsH = 30
      
      def init() (
        self.tabs @= []
        self.tabScroll = 0
        self.selectedTab = 0
        
        // .btoa().trim(1,-3)
        self.id = editor.editorI.toStr()
        self.i = editor.editorI
        editor.editorI ++
      )
      
      def createTab(string kind, string title, string content) (
        local dta @= {
          kind,
          title,
          content,
          id: OuidNew() ++ editor.tabI.toStr()
        }
        editor.tabI ++
        
        void self.tabs.append(dta)
        void self.updateTab(dta)
        self.selectedTab = self.tabs.len
        return dta
      )
      def openFile(string uuid) (
        local f @= open(uuid, [1,2,3,4])
        local t @= self.createTab("file", f[2], f[4])
        t.ohf @= ohf.getCompiledOhf(f[1])
        t.file_uuid = uuid
      )
      def updateTab(object tab) (
        inputs[self.id ++ tab.id] @= tab.content.split("\n")
      )
      def selectTab(number i) (
        self.selectedTab = i
        if self.tabs[self.selectedTab] != null (
          void self.updateTab(self.tabs[self.selectedTab])
        )
      )
      
      def update(array a) (
        goto area.centerX(a) area.centerY(a)
        square area.width(a) area.height(a) : c#prim
        if onclick (
          editor.selectedEditor = self.i
        )
        square area.width(a) - 3 area.height(a) - 3 : c#window_colour
        
        if self.selectedTab > self.tabs.len (
          void self.selectTab(self.tabs.len)
        )
        if self.selectedTab == 0 (
          void self.selectTab(1)
        )
        
        void self.updateMore([
          a[3] - 30,
          a[2],
          a[3],
          a[2] - self.tabsH
        ])
        
        void self.updateTabs([
          a[1],
          a[2],
          a[3] - 30,
          a[2] - self.tabsH
        ])
        
        void self.updateContent([
          a[1],
          a[2] - self.tabsH,
          a[3],
          a[4]
        ])
      )
      
      def updateMore(array a) (
        goto a[1] a[2]
        c prim
        pen "size" 1.5
        pen "down"
        goto a[1] a[4]
        goto a[3] a[4]
        pen "up"
        
        goto area.centerX(a) area.centerY(a)
        square area.width(a) - 3.5 area.height(a) - 3.5 : c#window_colour hover_c#prim
        if mouse_touching (
          cursor "pointer"
          if onclick (
            local p @= Promise.new(() -> (
              defer
              while mouse_down (
                defer
              )
              open_rightclick menus.editor.more()
            ))
            editor.selectedEditor = self.layoutI
          )
        )
        icon "more-vertical" .65 : c#txtc
      )
      
      def updateTabs(array a) (
        goto a[1] a[4]
        c prim
        pen "size" 1.5
        pen "down"
        goto a[3] a[4]
        pen "up"
        
        local width = 0
        for i self.tabs.len (
          local tab @= self.tabs[i]
          tab.width = tab.title.len * 8 + 30 + 30
          width += tab.width
        )
        
        frame a[1] a[2] a[3] a[4] 0 (
          goto 0 0
          square frame.width frame.height 0 0 1
          if mouse_touching (
            self.tabScroll += scroll.x.velocity + scroll.y.velocity * -1.5 * scroll.multiplier
          )
          self.tabScroll = max(min(self.tabScroll, width - frame.width), 0)
          
          local x = frame.left - self.tabScroll
          local rm = 0
          for i self.tabs.len (
            //log tab x
            local tab @= self.tabs[i]
            
            x += tab.width / 2
            
            goto x 0
            square tab.width frame.height 0 0 1
            if mouse_touching (
              if onclick (
                void self.selectTab(i)
              )
              goto x frame.bottom + 2
              square tab.width 1 : c#prim
            )
            
            if self.selectedTab == i (
              goto x frame.bottom + 2
              square tab.width 1 : c#seco
            )
            
            goto x 0
            centext tab.title 8 : c#txtc chx#-10
            
            x += tab.width / 2
            
            icn = "close"
            
            goto x - 20 0
            square 15 15 0 0 1
            hoverIcn = mouse_touching
            
            if tab.kind == "file" (
              local content = inputs[self.id ++ tab.id].join("\n")
              local edited = content != open(tab.file_uuid)
              
              if edited and !hoverIcn (
                icn = "w 17.5 dot 0 0"
              )
            )
            
            icon icn .5 : c#txtc
            if hoverIcn (
              cursor "pointer"
              if onclick (
                rm = i
              )
            )
            
            c prim
            pen "size" 1
            goto x frame.top
            pen "down"
            goto x frame.bottom
            pen "up"
          )
          
          if rm != 0 (
            void self.tabs.delete(rm)
          )
          
          if editor.selectedEditor == self.i (
            local selTab @= self.tabs[self.selectedTab]
            if selTab.kind == "file" (
              if "control".isKeyDown() and "s".onKeyDown() (
                file "open" selTab.file_uuid
                file "set" 4 selTab.content
                file "close"
              )
            )
          )
        )
      )
      
      def updateContent(array a) (
        if self.tabs.len == 0 (
          goto area.centerX(a) area.centerY(a)
          centext "no open files :(" 9 : c#prim
          return
        )
        
        void self.updateTextbox([
          a[1],
          a[2],
          a[3],
          a[4]
        ])
      )
      def updateTextbox(array a) (
        local tab @= self.tabs[self.selectedTab]
        
        if tab != null (
          goto area.centerX(a) area.centerY(a)
          textbox area.width(a) area.height(a) self.id ++ tab.id 0 {
            line_numbers: {
              bg_colour: prim,
              text_colour: txtc
            },
            sel_colour: seco,
            text_colour: txtc,
            ohf: tab.ohf
          }
          tab.content = inputs[self.id ++ tab.id].join("\n")
        )
      )
    )
  )
  return __imports__43f488fefabf2ce67b06a4b7a37dc437
case "./src/area.osl"
  class __imports__2d64d682ed6b046178c913d48fbef49a (
    def boundriesToArea(number left, number top, number right, number bottom) (
      return [left, top, right, bottom]
    )
    
    def copy(area) (
      return [area[1], area[2], area[3], area[4]]
    )
    
    def centerX(area) (
      return area[1] + area[3] / 2
    )
    
    def centerY(area) (
      return area[2] + area[4] / 2
    )
    
    def width(area) (
      return area[3] - area[1]
    )
    
    def height(area) (
      return area[2] - area[4]
    )
  )
  return __imports__2d64d682ed6b046178c913d48fbef49a)
)
import "window_tools" as "wt"
import as "glass" from "packages"

file "use_type" "osl"

// fpp boilerplate



object area @= import("./src/area.osl")
object editor @= import("./src/editor.osl")
object layout @= import("./src/layout.osl")
object icons @= import("./src/icons.osl")
object topbar @= import("./src/topbar.osl")
object state @= import("./src/state.osl")
object files @= import("./src/files.osl")
object ohf @= import("./src/ohf.osl")
object utils @= import("./src/utils.osl")
object menus @= import("./src/menus.osl")
object save @= import("./src/save.osl")
object settings @= import("./src/settings.osl")
object picker @= import("./src/picker.osl")
object popup @= import("./src/popup.osl")
object widgets @= {
  left: [
    import("./widgets/folder.osl"),
  ],
  right: [
    import("./widgets/run.osl"),
    //import("./widgets/test.osl"),
    //import("./widgets/build.osl")
  ]
}

boolean isPopup = passed_data.popup != null
object popupData @= passed_data.popup ?? {}

void save.init()
void settings.load()

//void state.loadFolder("08b90df05d7840ba31c0e71340b2043b")

if !window.permissions.contains("file admin") (
  permission "request" "file admin"
)

mainloop:
  wt:load_theme
  
  void picker.update()
  
  if isPopup (
    void popup.update()
  ) else (
    if file_dropped != 0 (
      local f @= open(file_dropped, [1,4,14])
      if f[1] == ".shortcut" (
        local f @= open(f[2], [1,4,14])
        file_dropped = f[3]
      )
      if files.hovered (
        if f[1] == ".folder" (
          void state.loadFolder(f[3])
        ) else (
          say "cannot drag non folder into files"
        )
      )
      file_dropped = 0
    )
    void layout.update()
    void topbar.update()
  )
void window.show()